apply plugin: 'com.android.application'
apply plugin: 'com.taobao.atlas'

group = 'com.horsege.atlastjavatest'
version = getEnvValue("versionName", "1.0.0");
def apVersion = getEnvValue("apVersion", "");

repositories {
    mavenLocal()
    jcenter()
}

String getEnvValue(key, defValue) {
    def val = System.getProperty(key);
    if (null != val) {
        return val;
    }
    val = System.getenv(key);
    if (null != val) {
        return val;
    }
    return defValue;
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.horsege.atlastjavatest"
        minSdkVersion 23
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            ndk {
                abiFilters "armeabi"
            }
        }
    }
}

atlas {
    atlasEnabled true

    tBuildConfig {
        autoStartBundles = ['com.horsege.firstbundle'] //自启动bundle配置
        preLaunch = 'com.horsege.atlastjavatest.AtlastTestPreLauncher'
        classInject false
    }

    manifestOptions {
        addAtlasProxyComponents true
    }


    patchConfigs {
        debug {
            createTPatch true
        }
    }


    buildTypes {
        debug {
            if (apVersion) {
                baseApDependency "com.horsege.android.atlasjavatest:AP-debug:${apVersion}@ap"
                patchConfig patchConfigs.debug
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.+'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'

    compile('com.taobao.android:atlas_core:5.0.7.30@aar') {
        transitive = true
    }

    compile project(':middleawaylibrary')
    compile project(':activitygroupcompat')

    bundleCompile project(':firstbundle')
}

apply plugin: 'maven'
apply plugin: 'maven-publish'

publishing {
    repositories {
        mavenLocal()
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact "${project.buildDir}/outputs/apk/${project.name}-debug.ap"
            artifactId "AP-debug"
        }
    }
}